m4_include(__MAKEFILE_DATA_DIR`/names.m4.conf')m4_dnl
m4_dnl
m4_dnl
m4_dnl
m4_define(`__OMNIMODS_CHOOSE_STYLE', `m4_define(`__OMNIMODS',m4_defn(`$1'))m4_undefine(`$1')')m4_dnl
m4_define(`__MOD_LIST_CHOOSE_STYLE', `m4_define(`__MOD_LIST',m4_defn(`$1'))m4_undefine(`$1')')m4_dnl
m4_define(`__MOD_CHOOSE_STYLE', `m4_define(`__MOD',m4_defn(`$1'))m4_undefine(`$1')')m4_dnl
m4_dnl
m4_dnl
m4_dnl
m4_define(`__MOD_LIST_ERLANG_STYLE',```[''$1`` {none, 0} ]''')m4_dnl
m4_define(`__MOD_ERLANG_STYLE',```{''$1``, ''$2``}, ''')m4_dnl
m4_define(`__OMNIMODS_ERLANG_STYLE',`
         #omnimods
         {
            attmods = $1'``,''`
            stamods = $2'``,''`
            atkmods = $3'``,''`
            defmods = $4
         }
')m4_dnl
m4_define(`__MOD_USE_ERLANG_STYLE',
   `__MOD_LIST_CHOOSE_STYLE(`__MOD_LIST_ERLANG_STYLE')__MOD_CHOOSE_STYLE(`__MOD_ERLANG_STYLE')__OMNIMODS_CHOOSE_STYLE(`__OMNIMODS_ERLANG_STYLE')')m4_dnl
m4_dnl
m4_dnl
m4_dnl
m4_define(`__MOD_LIST_JSON_STYLE',```[''$1`` {"t": "none", "v": 0} ]''')m4_dnl
m4_define(`__MOD_JSON_STYLE',```{"t": "''$1``", "v":'' $2``}, ''')m4_dnl
m4_define(`__OMNIMODS_JSON_STYLE',`
         {
            "attm": $1'``,''`
            "stam": $2'``,''`
            "atkm": $3'``,''`
            "defm": $4
         }
')m4_dnl
m4_define(`__MOD_USE_JSON_STYLE',
   `__MOD_LIST_CHOOSE_STYLE(`__MOD_LIST_JSON_STYLE')__MOD_CHOOSE_STYLE(`__MOD_JSON_STYLE')__OMNIMODS_CHOOSE_STYLE(`__OMNIMODS_JSON_STYLE')')m4_dnl
m4_dnl
m4_dnl
m4_dnl
m4_define(`__MOD_CONSTITUTION',  ``__MOD(__SN_CONSTITUTION(),   $1)'')m4_dnl
m4_define(`__MOD_DEXTERITY',     ``__MOD(__SN_DEXTERITY(),      $1)'')m4_dnl
m4_define(`__MOD_INTELLIGENCE',  ``__MOD(__SN_INTELLIGENCE(),   $1)'')m4_dnl
m4_define(`__MOD_MIND',          ``__MOD(__SN_MIND(),           $1)'')m4_dnl
m4_define(`__MOD_SPEED',         ``__MOD(__SN_SPEED(),          $1)'')m4_dnl
m4_define(`__MOD_STRENGTH',      ``__MOD(__SN_STRENGTH(),       $1)'')m4_dnl
m4_dnl
m4_dnl
m4_dnl
m4_define(`__MOD_SLASH',   ``__MOD(__SN_SLASH(),    $1)'')m4_dnl
m4_define(`__MOD_PIERCE',  ``__MOD(__SN_PIERCE(),   $1)'')m4_dnl
m4_define(`__MOD_BLUNT',   ``__MOD(__SN_BLUNT(),    $1)'')m4_dnl
m4_dnl
m4_dnl
m4_dnl
m4_define(`__MOD_MAX_HEALTH',       ``__MOD(__SN_MAX_HEALTH(),      $1)'')m4_dnl
m4_define(`__MOD_MOVEMENT_POINTS',  ``__MOD(__SN_MOVEMENT_POINTS(), $1)'')m4_dnl
m4_define(`__MOD_DODGE',            ``__MOD(__SN_DODGE(),           $1)'')m4_dnl
m4_define(`__MOD_PARRY',            ``__MOD(__SN_PARRY(),           $1)'')m4_dnl
m4_define(`__MOD_ACCURACY',         ``__MOD(__SN_ACCURACY(),        $1)'')m4_dnl
m4_define(`__MOD_DOUBLE_HITS',      ``__MOD(__SN_DOUBLE_HITS(),     $1)'')m4_dnl
m4_define(`__MOD_CRITICAL',         ``__MOD(__SN_CRITICAL_HIT(),    $1)'')m4_dnl
m4_dnl
m4_dnl
m4_dnl
m4_ifelse(__CODE_STYLE(),
   erlang, __MOD_USE_ERLANG_STYLE(),
   __CODE_STYLE, json, __MOD_USE_JSON_STYLE())m4_dnl
