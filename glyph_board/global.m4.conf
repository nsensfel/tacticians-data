m4_define(`__GLYPH_BOARD_CHOOSE_STYLE',
   `m4_define(`__GLYPH_BOARD',m4_defn(`$1'))m4_undefine(`$1')')m4_dnl
m4_define(`__GLYPH_SLOT_LIST',``[''$1``]'')m4_dnl
m4_define(`__GLYPH_SLOT', ```, $1''')m4_dnl
m4_define(`__FIRST_GLYPH_SLOT', `$1')m4_dnl
m4_dnl
m4_dnl
m4_dnl
m4_define(`__GLYPH_BOARD_ERLANG_STYLE',`from_id (<<"`$1'">>) ->
   #glyph_board
   {
      id = <<"$1">>,
      name = <<"$2">>,
      slots = $3
   };
')m4_dnl
m4_define(`__GLYPH_BOARD_USE_ERLANG_STYLE',
   `__GLYPH_BOARD_CHOOSE_STYLE(`__GLYPH_BOARD_ERLANG_STYLE')')m4_dnl
m4_dnl
m4_dnl
m4_dnl
m4_define(`__GLYPH_BOARD_JSON_STYLE',`   {
      "msg": "add_glyph_board",
      "id": "$1",
      "nam": "$2",
      "slot": $3
   },
')m4_dnl
m4_define(`__GLYPH_BOARD_USE_JSON_STYLE',
   `__GLYPH_BOARD_CHOOSE_STYLE(`__GLYPH_BOARD_JSON_STYLE')')m4_dnl
m4_dnl
m4_dnl
m4_dnl
m4_ifelse(__CODE_STYLE(),
   erlang, `__GLYPH_BOARD_USE_ERLANG_STYLE()',
   __CODE_STYLE(), json, `__GLYPH_BOARD_USE_JSON_STYLE()')m4_dnl
