m4_include(__MAKEFILE_DATA_DIR`/modifiers.m4.conf')m4_dnl
m4_define(`__ARMOR_CHOOSE_STYLE', `m4_define(`__ARMOR',m4_defn(`$1'))m4_undefine(`$1')')m4_dnl
m4_define(`__ARMOR_ERLANG_STYLE',`from_id (`$1') ->
   #armor
   {
      id = $1,
      name = <<"$2">>,
      attr_mod = $3,
      stat_mod = $4,
      atk_mod = $5,
      def_mod = $6
   };
')m4_dnl
m4_define(`__ARMOR_JSON_STYLE',`   {
      "msg": "add_armor",
      "id": $1,
      "nam": "$2",
      "attm": $3,
      "stam": $4,
      "atkm": $5,
      "defm": $6
   },
')m4_dnl
m4_define(`__ARMOR_USE_ERLANG_STYLE',`__ARMOR_CHOOSE_STYLE(`__ARMOR_ERLANG_STYLE')__MOD_USE_ERLANG_STYLE()')m4_dnl
m4_define(`__ARMOR_USE_JSON_STYLE',`__ARMOR_CHOOSE_STYLE(`__ARMOR_JSON_STYLE')__MOD_USE_JSON_STYLE()')m4_dnl
